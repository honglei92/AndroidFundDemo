apply plugin: 'com.android.application'
android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    /**
     * 一、默认产品偏好配置
     */
    defaultConfig {
        applicationId "com.boco.whl.funddemo"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi-v7a'
        }
    }

    /**
     * 二、自定义签名配置
     */
    signingConfigs {
        release {
            try {
                storeFile file("./keystore/release.keystore")
                storePassword "testres"
                keyAlias "testres"
                keyPassword "testres"
            } catch (ex) {
                throw new InvalidUserDataException(ex.toString())
            }
        }

        debug {
            storeFile file("./keystore/debug.keystore")
        }
    }
    /**
     * 三、构建类型，分为release和debug两种
     */

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                if (variant.buildType.name != "debug") {
                    outputFileName = "FUND_DEMO_V" +
                            variant.versionName + "_" +
                            releaseTime() + "_" +
                            variant.buildType.name + "_" +
                            variant.signingConfig.name +
                            ".apk"
                }
            }
        }
    }
    /**
     * 四、自定义产品偏好配置，可以定义多个偏好产品
     */

    /**
     * 五、DEX文件构建属性配置（加快构建速度）
     */
    dexOptions {
        //incremental = true;
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }
    /**
     * 六、将一个apk拆分成多个相关配置（拆分依据：屏幕密度、系统架构）
     */
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            jniLibs.srcDirs = ['src/main/libs']
            resources.srcDirs = ['src']
            assets.srcDirs = ['src/main/assets']
            jni.srcDirs = []
        }
    }
}
/**
 七、引入依赖包的秘密
 */
dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api fileTree(dir: 'libs/guangdong', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    //网络相关
    api 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    api 'com.squareup.retrofit2:retrofit:2.2.0'
    api 'com.squareup.retrofit2:retrofit-converters:2.2.0'
    api 'com.squareup.retrofit2:retrofit-adapters:2.2.0'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    api 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    api 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    api 'io.reactivex.rxjava2:rxjava:2.1.0'
    api 'io.reactivex.rxjava2:rxandroid:2.0.1'
    api 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    // DBFlow数据库
    api 'com.github.Raizlabs.DBFlow:dbflow-core:4.0.4'
    api 'com.github.Raizlabs.DBFlow:dbflow:4.0.4'
    api 'com.github.Raizlabs.DBFlow:dbflow-rx2:4.0.4'
    //图片相关
    api 'com.github.bumptech.glide:glide:3.7.0'
    api 'com.contrarywind:Android-PickerView:3.2.4'
    api 'com.airbnb.android:lottie:2.5.5'
    //布局相关
    api 'com.android.support.constraint:constraint-layout:1.1.2'
    api 'com.android.support:appcompat-v7:27.1.1'
    api 'com.android.support:recyclerview-v7:27.1.1'
    api 'com.android.support:design:27.1.1'
    api 'com.github.sun-sky:Marquee:1.0.0'
    api 'de.halfbit:pinned-section-listview:1.0.0'
    //工具相关
    api 'org.greenrobot:eventbus:3.0.0'
    api 'com.google.code.gson:gson:2.8.0'
    api 'com.blankj:utilcode:1.7.1'
    api 'com.alibaba:fastjson:1.2.47'
    api 'com.orhanobut:logger:2.1.1'
    api "com.google.guava:guava:$rootProject.guavaVersion"
    api 'com.jakewharton:butterknife:8.4.0'
    api 'com.android.support:multidex:1.0.3'
    //日志相关
//    api 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    debugApi 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseApi 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    testImplementation 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    annotationProcessor 'com.github.Raizlabs.DBFlow:dbflow-processor:4.0.4'
}
// 获取当前系统时间
static def releaseTime() {
    String data = new Date().format("yyyy-MM-dd HH:mm", TimeZone.getTimeZone("GMT+08:00"))
    data = data.replace(":", "")
    data = data.replace("-", "")
    data = data.replace(" ", "")
    data = data.substring(2)
    return data
}
